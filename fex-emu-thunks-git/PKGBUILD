# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

pkgname=fex-emu-thunks-git
pkgver=r3351.d3cd354c
pkgrel=1
pkgdesc="Fex special guest libraries that call out to host code for speed and compatibility"
arch=('aarch64')
url="https://fex-emu.org/"
license=('MIT')
depends=('mesa')
makedepends=(
  'cmake' 'ninja' 'clang' 'mesa' 'x86_64-gcc'
  'x86_64-mesa' 'x86_64-alsa-lib' 'x86_64-libxrandr'
)
options=(!strip)
conflicts=('fex-emu-thunks')
provides=('fex-emu-thunks')
source=("$pkgname::git+https://github.com/FEX-Emu/FEX.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname
  git submodule update --init --recursive

  if [ -e "../$pkgname-build" ]; then
    rm -rf "../$pkgname-build"
  fi

  cd ../ && mkdir $pkgname-build
}

build() {
  cd $pkgname-build

  unset CFLAGS
  unset CPPFLAGS
  unset CXXFLAGS
  unset LDFLAGS

  CC=clang CXX=clang++ cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release -DENABLE_LTO=False \
    -DENABLE_STATIC_PIE=False -DBUILD_TESTS=False \
    -DENABLE_ASSERTIONS=False -DBUILD_THUNKS=True \
    -G Ninja ../$pkgname

  ninja guest-libs host-libs
}

package() {
   cd $pkgname-build

   mkdir -p "$pkgdir"/usr/lib/fex-emu/HostThunks/
   mkdir -p "$pkgdir"/usr/lib/fex-emu/GuestThunks/

   cp -v Host/*.so "$pkgdir"/usr/lib/fex-emu/HostThunks/
   cp -v Guest/*.so "$pkgdir"/usr/lib/fex-emu/GuestThunks/
}
