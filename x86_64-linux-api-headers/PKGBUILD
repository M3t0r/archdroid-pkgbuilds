# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Christer Solskogen <christer.solskogen@gmail.com>

_arch=x86_64
_target=$_arch-linux-gnu
pkgname=$_arch-linux-api-headers
pkgver=5.12.3
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace ($_arch)"
arch=(any)
url='https://www.kernel.org'
license=(GPL2)
makedepends=(rsync)
source=(https://www.kernel.org/pub/linux/kernel/v5.x/linux-$pkgver.tar.xz)
sha256sums=('133714a87a8b0ed1959587a8a5aff9faf47dd0fc8feeac08cca408f7e392878d')


build() {
  cd linux-$pkgver

  make ARCH=$_arch mrproper
  make ARCH=$_arch headers_check
}

package() {
  cd linux-$pkgver

  make INSTALL_HDR_PATH="$pkgdir/usr/$_target/sys-root/usr" ARCH=$_arch V=0 headers_install

  # clean-up unnecessary files generated during install
  find "$pkgdir" \( -name .install -or -name ..install.cmd \) -delete
}
